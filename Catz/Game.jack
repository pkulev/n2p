/** Game */

class Game {

    // Controllable character
    field Cat actor;

    // Spawn places (no time for random)
    field Array foodSpawns;

    // References to food objects
    field Array foods;

    // Game loop relative speed
    field int tick;

    // Idle ticks counter (loop calls) to track idle animation
    field int idle;

    // Is game running?
    field boolean running;

    // Border
    static int width;
    static int height;

    constructor Game new() {

        let width = 512;
        let height = 256;

        // bottom, minus sprite height
        let actor = Cat.new(256-16, 0);

        let tick = 10; // 10 ticks ~= 0.1s on my machine
        let idle = 0;
        let running = true;

        let foodSpawns = Array.new(6);
        let foods = Array.new(6);
        do Game.fillFoodSpawns(6, foodSpawns, foods);

        return this;
    }

    function void fillFoodSpawns(int len, Array spawns, Array foods) {
        var int i;
        var Array tmp;
        let i = 0;
        while (~(i = len)) {
            let spawns[i] = Vec.new(0, 50 * i);
            let tmp = spawns[i];
            let foods[i] = Food.new(i / 2, tmp[0], tmp[1]);
            let i = i + 1;
        }
        return;
    }


    method void dispose() {
        do actor.dispose();
        do foodSpawns.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /* Exit. */
    method void exit() {
        let running = false;
        return;
    }

    /* Handle events. */
    method void events() {
        var char key;
        // amount of ticks after set idle animation for actor
        var int maxIdle;
        // after what amount of idle cycles to stop moving
        var int stopMotionIdle;

        let key = Keyboard.keyPressed();
        let maxIdle = tick * 40;  // ~4s
        let stopMotionIdle = tick * 5;  // ~0.5s

        // increase idle ticks counter
        if (~key) {
            let idle = idle + 1;
            // Activate idle animation
            if (idle > maxIdle) {
                let idle = maxIdle;  // prevent int overflow
                do actor.stop();
            } else {
                // Not max idle but enough to stop motion
                if (idle > stopMotionIdle) {
                    do actor.stopMotion();
                }
            }
        }

        // if (key = 32) { // space
        if (key = 81) {do exit();}  // q
        if (key = 130) {do actor.moveLeft(); let idle = 0;}  // left arrow
        if (key = 132) {do actor.moveRight(); let idle = 0;}  // right arrow
        return;
    }

    /* Update objects' state. */
    method void update() {
        do actor.update();
        return;
    }

    /* Render objects to screen. */
    method void render() {
        do actor.render();
        return;
    }

    /* Main loop of the application. */
    method void loop() {

        while (running) {
            do events();
            do update();
            do render();

            do Sys.wait(tick);
        }
        return;
    }
}
